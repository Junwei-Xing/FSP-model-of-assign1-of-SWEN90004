/**
Author: XING JUNWEI, student ID: 745568, this FSP model is for simulating the process of a1's solution1.
*/
//these three integers are for the transfer of conditions of each segment of belt and robot arm.
const NONEBIKE = 0
const UNTAGGEDBIKE = 1
const TAGGEDBIKE = 2

range MYRANGE = NONEBIKE..TAGGEDBIKE

//create a tagged bike or a untagged bike or leaving a empty on the segment 1 as a producer.
PRODUCER = ( create_tagged -> PRODUCER|
			create_untagged -> PRODUCER|
			create_null -> PRODUCER).

//each parameter represents a corresponding segment of belt.
BELT=BELT[NONEBIKE][NONEBIKE][NONEBIKE][NONEBIKE][NONEBIKE],
BELT[s1:MYRANGE][s2:MYRANGE][s3:MYRANGE][s4:MYRANGE][s5:MYRANGE]=(

//if the segment 1 is empty now, allows that producer can create a bike to there.
when(s1==NONEBIKE) create_null -> BELT[NONEBIKE][s2][s3][s4][s5]|
when(s1==NONEBIKE) create_untagged -> BELT[UNTAGGEDBIKE][s2][s3][s4][s5]|
when(s1==NONEBIKE) create_tagged -> BELT[TAGGEDBIKE][s2][s3][s4][s5]|

//beltmover; move, if there is no bike on the segment 5, if there is no a tagged bike is waiting on the segment 5, if there are at least 1 bike on the belt.
when(s5 == NONEBIKE && s3<TAGGEDBIKE && (s1+s2+s3+s4+s5>0)) move -> BELT[NONEBIKE][s1][s2][s3][s4]|

//when detecting there is a tagged bike on the segment 3, acquiring it by robot arm.
when(s3 == TAGGEDBIKE) getting -> BELT[s1][s2][NONEBIKE][s4][s5]|

//after processing the tagged bike, put it back to segment 3.
when(s3 == NONEBIKE) backing -> BELT[s1][s2][UNTAGGEDBIKE][s4][s5]|

//consumer removes the untagged bike on the segment 5.
when(s5 == UNTAGGEDBIKE) remove -> BELT[s1][s2][s3][s4][NONEBIKE]

).

ROBOTARM = ROBOTARM[NONEBIKE],
ROBOTARM[s:MYRANGE] = (
/* the deadlock will occur in this part. when these two actions form a conflict.
if there is empty, means robot arm is able to getting a tagged bike from segment 3 to inspctor.
or backing a untagged bike (has been processed) from inspector to segment 3.
**/
when(s == NONEBIKE) getting -> ROBOTARM[TAGGEDBIKE]|
when(s == NONEBIKE) sendingToBelt -> ROBOTARM[UNTAGGEDBIKE]|

//this action links with the backing operation at segment 3, put the processed bike back.
when(s == UNTAGGEDBIKE) backing -> ROBOTARM[NONEBIKE]|
//this action links with inspect when robot arm got a tagged bike, asking for inspection.
when(s == TAGGEDBIKE) sendingToInspector -> ROBOTARM[NONEBIKE]
).

//Inspector removed tag from the tagged bike.
INSPECTOR = (sendingToInspector -> removeTag -> sendingToBelt -> INSPECTOR).

//Process consumer.
CONSUMER = (remove -> CONSUMER).

//check the deadlock.
property SAFETY1 = (getting -> removeTag -> backing -> SAFETY1).

//check the progress of robot arm
progress PROGRESS1 = {getting, sendingToBelt, backing, sendingToInspector}

//concurrency.
||MYSYSTEM = (PRODUCER||BELT||ROBOTARM||INSPECTOR||CONSUMER||SAFETY1).
